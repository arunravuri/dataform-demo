config {
  type: "table",
  name: "th_payment_reports",
  tags: ["th_payment_reports", "monthly"],
  columns: {
    batch_id: "Batch identifier",
    batch_name: "Batch name",
    completed_date: "Batch Completed date",
    source_language: "source languge",
    target_language: "target language",
    language_name: "Target language name",
    new_source_segment_word_count: "word count from source segments",
    bullets_word_count: "word count of bullets text in parent_deliverable_source_field",
    prose_word_count: "word count of text other than bullets in parent_deliverable_source_field"
  },
  assertions: {
    nonNull: [
        "batch_id",
        "target_language"
    ]
  },
  schema: dataform.projectConfig.vars.reportDataset
}

SELECT
    cb.batch_id,
    b.batch_name,
    cb.completed_date,
    ns.source_language,
    ns.target_language,
    l.language_name,
    ns.new_source_segment_word_count,
    ts.bullets_word_count,
    ts.prose_word_count
FROM
    ${ref("th_completed_batches")} cb
    JOIN
        ${ref("th_new_source_segments")} ns ON cb.batch_id = ns.batch_id
    JOIN
        ${ref("th_total_source_words")} ts ON ts.batch_id = cb.batch_id 
            AND ts.language_code = ns.target_language
    JOIN
        ${ref("batch")} b ON b.batch_id = cb.batch_id
    JOIN
        ${ref("language")} l ON ns.target_language = l.language_code
ORDER BY
    cb.completed_date DESC,
    cb.batch_id ASC
