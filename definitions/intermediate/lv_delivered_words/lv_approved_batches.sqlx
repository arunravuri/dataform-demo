config {
    type: "view",
    name: "lv_approved_batches",
    tags: ["lv_delivered_words_report", "monthly"],
    columns: {
        batch_id: "Batch Identifier",
        approved_month: "Deliverable first time approved month"
    },
    schema: dataform.projectConfig.vars.reportDataset
}

SELECT
    DISTINCT
    pd.batch_id,
    FORMAT_TIMESTAMP('%Y-%m', tl.create_date) AS approved_month
FROM
    ${ref("transition_log")} tl
JOIN
    ${ref("deliverable")} d ON d.deliverable_id = tl.deliverable_id
JOIN
    ${ref("parent_deliverable")} pd ON pd.parent_deliverable_id = d.parent_deliverable_id
JOIN
    ${ref("batch")} b ON pd.batch_id = b.batch_id
JOIN
    ${ref("project")} p ON p.project_id = b.project_id
JOIN
    ${ref("order_form")} ofc ON p.order_form_id = ofc.order_form_id
JOIN
    ${ref("account")} a ON a.account_id = ofc.account_id
JOIN
    ${ref("parent_account")} pa ON pa.parent_account_id = a.parent_account_id
JOIN
    ${ref("stage")} s_from ON s_from.stage_id = tl.from_stage_id
JOIN
    ${ref("stage")} s_to ON s_to.stage_id = tl.to_stage_id
WHERE
    d.archived = 0
    AND a.parent_account_id = 51
    AND pd.archived = 0
    AND d.allocated = 1
    AND s_from.stage_type IN ('Production', 'Training')
    AND s_to.stage_type = 'Client'
    AND tl.transition_log_id IN (
        SELECT
            MIN(tl2.transition_log_id)
        FROM
            ${ref("transition_log")} tl2
        JOIN
            ${ref("stage")} s2a ON tl2.from_stage_id = s2a.stage_id
        JOIN
            ${ref("stage")} s2b ON tl2.to_stage_id = s2b.stage_id
        JOIN
            ${ref("deliverable")} d2 ON d2.deliverable_id = tl2.deliverable_id
        WHERE
            tl2.deliverable_id = tl.deliverable_id
            AND d2.language_code = d.language_code
            AND s2a.stage_type IN ('Production', 'Training')
            AND s2b.stage_type = 'Client'
    )
